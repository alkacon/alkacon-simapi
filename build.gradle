apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = java_target_version
targetCompatibility = java_target_version

repositories {
    mavenLocal()
    if (project.hasProperty('additional_repositories')){
        additional_repositories.split(';').each{ repo ->
            maven { url repo }
        }
    }
    mavenCentral()
}

configurations {
    compile {
        description = 'Includes all dependencies required for compile and runtime'
    }
    testCompile {
        description = 'Includes all dependencies required for testing'
    }
}

// import dependencies
apply from: 'dependencies.gradle'

sourceSets{
    main {
        java.srcDir 'src'
        resources {
            srcDir 'src'
            exclude '**/*.java'
        }
    }
    
    test {
        java.srcDir 'test'
        resources {
            srcDir 'test'
            exclude '**/*.java'
        }
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': product_name,
        'Implementation-Version': version
    }
    exclude '**/.gitignore'
}

javadoc {
    project.configure(options) {
        memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
        charSet = "UTF-8"
        docTitle = "$product_name"
        windowTitle = "$product_name"
        header = "<b>$product_name</b>"
        author = "true"
        use = "true"
        source= java_target_version
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from "${buildDir}/docs/javadoc"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

test {
    useJUnit()
    include 'com/alkacon/simapi/AllTests*'
    // important: exclude all anonymous classes
    exclude '**/*$*.class'
    scanForTestClasses false
    testClassesDirs = sourceSets.test.output.classesDirs
    testLogging.showStandardStreams = true
    ignoreFailures true
}

artifacts {
    archives jar
    tasks.each{ task ->
        if (task.name.endsWith("Jar")){
            archives task
        }
    }
}

signing { 
    required {gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives 
}

if (project.hasProperty('publish_repository')){
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: publish_repository) {
                    authentication(userName: publish_user, password: publish_password)
                }
                artifacts.each{ arch ->
                    def filterName=arch.name
                    filterName=filterName.substring(0, filterName.lastIndexOf("-"))
                    addFilter(filterName){artifact, file ->
                        artifact.name.startsWith(filterName)
                    }
                    pom(filterName) {
                        // we do not want test dependencies in the POM
                        whenConfigured { p ->
                            p.dependencies = p.dependencies.findAll { dep -> dep.scope != "test" };
                        }

                        project {
                            name product_name
                            packaging 'jar'
                            description product_description
                            groupId group_id
                            url "http://www.opencms.org"
                            scm {
                                url "scm:git@github.com:alkacon/${artifact_name}.git"
                                connection "scm:git@github.com:alkacon/${artifact_name}.git"
                                developerConnection "scm:git@github.com:alkacon/${artifact_name}.git"
                            }
                            licenses {
                                license {
                                    name 'GNU Lesser General Public License'
                                    url 'http://www.gnu.org/licenses/lgpl.html'
                                    distribution 'repo'
                                }
                            }
                            organization {
                                name 'Alkacon Software'
                                url 'http://www.alkacon.com'
                            }
                            developers {
                                developer {
                                    name 'Alkacon Software'
                                    url 'http://www.alkacon.com'
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

install {
    repositories {
        mavenInstaller {

            artifacts.each{ arch ->
                def filterName=arch.name
                filterName=filterName.substring(0, filterName.lastIndexOf("-"))
                addFilter(filterName){artifact, file ->
                    artifact.name.startsWith(filterName)
                }

               pom(filterName) {
                    // we do not want test dependencies in the POM
                    whenConfigured { p ->
                        p.dependencies = p.dependencies.findAll { dep -> dep.scope != "test" };
                    }
                    project {
                        name product_name
                        packaging 'jar'
                        description product_description
                        groupId group_id
                        url "http://www.opencms.org"
                        scm {
                            url "scm:git@github.com:alkacon/${artifact_name}.git"
                            connection "scm:git@github.com:alkacon/${artifact_name}.git"
                            developerConnection "scm:git@github.com:alkacon/${artifact_name}.git"
                        }
                        licenses {
                            license {
                                name 'GNU Lesser General Public License'
                                url 'http://www.gnu.org/licenses/lgpl.html'
                                distribution 'repo'
                            }
                        }
                        organization {
                            name 'Alkacon Software'
                            url 'http://www.alkacon.com'
                        }
                        developers {
                            developer {
                                name 'Alkacon Software'
                                url 'http://www.alkacon.com'
                            }
                        }
                    }
                }
            }

        }
    }
}

tasks.withType(Javadoc) {
    if (JavaVersion.current().isJava8Compatible()) {
    	options.addStringOption("Xdoclint:none", "-quiet")
    }
}
